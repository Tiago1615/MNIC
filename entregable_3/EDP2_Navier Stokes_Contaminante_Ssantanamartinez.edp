// Modelo de transporte-difusion con termino de reaccion
// Esquema con derivada material y refinamiento adaptativo

// Construccion del dominio computacional
// Geometria: canal con techo ondulado
border a(t=0,2) {x=t;y=0;label=1;};
border b(t=0,0.5) {x=2;y=t;label=5;}; 
border c(t=2,0) {x=t;y=0.375+0.125*sin(pi*(t+0.5));label=3;};
border d(t=0.5,0) {x=0;y=t;label=4;};


int n=80;
mesh malla = buildmesh(a(n)+b(n)+c(n)+d(n));
plot(malla, wait=1);

// Declaracion de espacios funcionales
fespace Vh(malla,P2);
Vh u,v,kx,ky,u1,u2,uoldt,uu;

// Parametros de difusion del medio
kx=0.02624;  
ky=0.02624;

real densidad,cp;
densidad=1.1769;
cp=1.0073;
real A;
A= densidad*cp;

// Campo de velocidades del flujo
u1=-48*y*(y - 0.5);
u2=0;

real dt=0.01;
real r=0.001; // tasa de decaimiento

// Distribucion inicial del contaminante
real AA=100000000;
real h=1000;
real aa=1;
real bb=0.125;

uoldt = AA*exp(-h*((x-aa)^2 +(y-bb)^2));

real alpha=1/dt;

// Formulacion variacional del problema
problem calor(u,v) = 
int2d (malla) (A*u*v/dt)- int2d(malla) (A*alpha*convect([u1,u2],-dt,uoldt)*v)
+ int2d(malla)(kx*dx(u)*dx(v)+ky*dy(u)*dy(v))
+ int2d(malla)(r*u*v)
+ on(4,u=0);
plot([u1,u2], wait=1);


int m=0;
real tfinal=10;

int iter;

for (real t=0;t<tfinal;t += dt)
{
    iter=iter+1;
    calor;
    uoldt=u; 


    plot(u, cmm=" tiempo ="+t, value=1, WindowIndex=0);

    malla = adaptmesh(
        malla, 
        u,
        err=0.01,
        hmax=0.1,
        hmin=0.001,
        cutoff=1e-6,
        ratio=1.6,
        nbvx=20000,
        splitpbedge=2,
        power=1,
        abserror=true,
        nomeshgeneration=false,
        anisomax=1
    );

    plot(malla, cmm=" tiempo ="+t, WindowIndex=1);;

} 


