// difusion transporte con implementacion derivada parcial, cond conctorno de conveccion a la salida

//Definimos el dominio de estudio, creamos la malla y la dibujamos
//----------------------------------------------------------------

// Dimensiones del dominio
real L=2.0;
real H=0.5;

//Pared inferior
border a(t=0, L) {
    x=t;
    y=0;
    label=1;
};

//Salida de aire
border b(t=0, H) {
    x=L;
    y=t;
    label=5;
};

// Pared superior
border c(t=L, 0) {
    x = t;
    y = 0.375 + 0.125 * (sin(pi * (x + 0.5)));
    label = 3;
};

//Entrada de aire
border d(t=H, 0) {
    x=0;
    y=t;
    label=4;
};


int n=80;
mesh malla = buildmesh(a(n) + b(n) + c(n) + d(n));

//Definimos el problema que queremos resolver
//-------------------------------------------
fespace Vh(malla,P2);
Vh u,v,kx,ky,u1,u2,uoldt,uu; // u es temperatura en grados Celsius  , añadido uoldt que es la solucion u en la iteracion anterior en bucle abajo

//Conductividad térmica constante
kx=0.02624;  
ky=0.02624;
real hc=10.; //Coeficiente de convección   W/m2

// temperatura del aire exterior a la salida
real uf = 25.; 

real densidad,cp;
densidad=1.1769;
cp=1.0073;
real A;
A= densidad*cp;

// umax = 3 m/s en y = H/2 --> u(y) = C * y * (H - y) --> 3 = C * (H/2) * (H - H/2) --> C = 3 / (H^2/4) = 12 / H^2 = 12 / 0.25 = 48
u1 = 48.0*y*(H-y);          // componente horizontal del vector velocidad
u2 = 0;                       // u2 es cero, la componente vertical del vector velocidad es cero 

real dt=0.01;
real alpha=1/dt;

problem calor(u,v) = 
    int2d (malla) (A * u * v/dt)
    - int2d(malla) (A * alpha * convect([u1,u2], -dt, uoldt) * v) //derivada total
    + int2d(malla)(kx * dx(u) * dx(v) + ky * dy(u) * dy(v))
    + int1d(malla, 5) (hc * u * v) - int1d(malla, 5) (hc * uf * v)
    + on(4, u = 15);  // aire entra a 15 grados
int m = 0;
real tfinal = 3; // tiempo final de simulacion 
int iter;
for (real t=0; t < tfinal; t += dt)
{
    iter = iter + 1;  // contador de iteraciones
    calor;            // CALCULA u
    uoldt = u; 

    if (!(iter % 4))
    plot(u, cmm=" tiempo =" +t, value=1);

    malla = adaptmesh(
        malla,
        u,
        err=0.01,
        hmax=0.1,
        hmin=0.001,
        cutoff=1e-6,
        ratio=1.6,
        nbvx=40000,
        splitpbedge=2,
        power=1,
        abserror=true,
        nomeshgeneration=false,
        anisomax=1
    );
} 
